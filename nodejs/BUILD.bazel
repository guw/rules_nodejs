load("@bazel_skylib//:bzl_library.bzl", "bzl_library")
load("//nodejs/private:nodejs_toolchains_repo.bzl", "PLATFORMS")
load("//nodejs/private:user_build_settings.bzl", "user_args")
load(":node_toolchain_alias.bzl", "node_host_runtime_alias", "node_runtime_alias", "node_toolchain_alias")

package(default_visibility = ["//visibility:public"])

exports_files([
    "index.for_docs.bzl",
    "providers.bzl",
])

bzl_library(
    name = "index.for_docs",
    srcs = glob(["*.bzl"]) + ["@bazel_tools//tools:bzl_srcs"],
    deps = [
        "//nodejs/private:bzl",
        "//nodejs/private/providers:bzl",
        "@bazel_skylib//lib:paths",
    ],
)

# A single binary distribution of a Node provides two different types of toolchains from the
# perspective of Bazel:

# (1) The transpilation toolchain, which provides the Node runtime used to execute the transpiler
# (and type checker), as well as various helper tools and settings.
#
# Toolchains of this type typically have constraints on the execution platform so that their Node
# runtime can run the transpiler, but not on the target platform as Node transpilation outputs are
# platform independent.
#
# Obtain the associated NodeInfo via:
#   ctx.toolchains["@bazel_tools//tools/jdk:toolchain_type"].nodeinfo
toolchain_type(name = "toolchain_type")

# (2) The Node runtime that executable Node outputs (e.g., js_binary) will run on.
#
# Toolchains of this type typically have constraints on the target platform so that the runtime's
# native 'node' binary can be run there, but not on the execution platform as building an executable
# Node target only requires copying or symlinking the runtime, which can be done on any platform.
#
# Obtain the associated NodeRuntimeInfo via:
#   ctx.toolchains["@bazel_tools//tools/jdk:runtime_toolchain_type"].nodeinfo
toolchain_type(name = "runtime_toolchain_type")

# Points to toolchain[":runtime_toolchain_type"]
node_runtime_alias(name = "current_node_runtime")

# Host configuration of ":current_node_runtime"
node_host_runtime_alias(name = "current_host_node_runtime")

# Points to toolchain[":toolchain_type"]
node_toolchain_alias(name = "current_node_toolchain")

# The platforms that are supported by the Node toolchains.
[
    platform(
        name = key,
        constraint_values = values.compatible_with,
    )
    for key, values in PLATFORMS.items()
]

# Default arguments/flags that are passed to nodejs in all nodejs_binary and
# nodejs_test targets. Can be overwritten by settings
# --@rules_nodejs//nodejs:default_args="--flag1 --flag2"
user_args(
    name = "default_args",
    build_setting_default = "--preserve-symlinks",
)
